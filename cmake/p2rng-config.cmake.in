@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/modules)

include ( "${CMAKE_CURRENT_LIST_DIR}/p2rng-targets.cmake" )

foreach(_comp ${p2rng_FIND_COMPONENTS})
  if (${_comp} STREQUAL cuda)
    enable_language(CUDA)
    find_dependency(CUDAToolkit 11 REQUIRED)
  elseif(${_comp} STREQUAL oneapi)
    find_dependency(IntelDPCPP REQUIRED)
    find_dependency(oneDPL REQUIRED)
    if("${CMAKE_CXX_COMPILER}" MATCHES ".*clang\\+\\+")
      set(SYCL_TARGETS "nvptx64-nvidia-cuda,amdgcn-amd-amdhsa,spir64")
      set(SYCL_CUDA_ARCH "sm_50")
      set(SYCL_ROCM_ARCH "gfx1030")
    else()
      set(SYCL_TARGETS "spir64")
    endif()
  elseif(${_comp} STREQUAL rocm)
    find_dependency(hip REQUIRED)
    find_dependency(rocThrust REQUIRED)
    set(HIP_TARGET "hip::device" CACHE
        STRING "Choose HIP target ?"
    )
    set_property(CACHE HIP_TARGET PROPERTY STRINGS
      "hip::host"
      "hip::device"
    )
  elseif(${_comp} STREQUAL openmp)
    find_dependency(OpenMP REQUIRED)
  else()
    set(p2rng_FOUND False)
    set(p2rng_NOT_FOUND_MESSAGE "Unsupported p2rng target API: ${_comp}")
  endif()
  include("${CMAKE_CURRENT_LIST_DIR}/p2rng-${_comp}-targets.cmake")
endforeach()
